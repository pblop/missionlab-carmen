/**********************************************************************
 **                                                                  **
 **              COMMUNICATION_SENSITIVE_BEHAVIOR.cnl                **
 **                                                                  **
 **  Written by:  Yoichiro Endo                                      **
 **                                                                  **
 **  This is a behavioral set that is used for the communication     **
 **  sensitive behavior that is developed for MARS 2020 project.     **
 **                                                                  **
 **  Copyright 2004 2006 Georgia Tech Research Corporation           **
 **  Atlanta, Georgia  30332-0415                                    **
 **  ALL RIGHTS RESERVED, See file COPYRIGHT for details.            **
 **                                                                  **
 **********************************************************************/

/* $Id: COMMUNICATION_SENSITIVE_BEHAVIOR.cnl,v 1.1.1.1 2006/07/12 13:37:58 endo Exp $ */

init
#include "CommBehaviorAdviseMethod.h"
iend

#include "cnl.inc"

//-------------------------------------------------------------------------
// This function returns a vector that is a copy of the communication
// sensitive behavioral advise that is executed here.
//-------------------------------------------------------------------------
procedure Vector FOLLOW_CSB_ADVISE with
int method;
header
body
{
    CSB_Advise_t advise;
    int adviseMethod = CSB_ADVISE_GREEDY;

    // Set the advise method.
    switch (method) {

    case CSB_Greedy:
        adviseMethod = CSB_ADVISE_GREEDY;
        break;

    case CSB_Average:
        adviseMethod = CSB_ADVISE_AVERAGE;
        break;

    case Comm_Recovery:
        adviseMethod = CSB_ADVISE_USE_COMM_RECOVERY;
        break;

    case Comm_Preserve:
        adviseMethod = CSB_ADVISE_USE_COMM_PRESERVE;
        break;

    case Internalized_Plan:
        adviseMethod = CSB_ADVISE_USE_INTERNALIZED_PLAN;
        break;
    }

    VECTOR_CLEAR(output);

	exec_update_csb_sensor_data();

    // Get the advise.
    advise = exec_get_csb_advise(adviseMethod);

    output.x = advise.x;
    output.y = advise.y;
}
pend

//-------------------------------------------------------------------------
// This function initializes the communication sensitive behavior.
//-------------------------------------------------------------------------
procedure Vector INIT_CSB with
header
body
{
    VECTOR_CLEAR(output);

    exec_init_csb();
}
pend

//-------------------------------------------------------------------------
// This function updates the sensor data.
//-------------------------------------------------------------------------
procedure Vector UPDATE_CSB_SENSOR_DATA with
header
body
{
    VECTOR_CLEAR(output);

    exec_update_csb_sensor_data();
}
pend

//-------------------------------------------------------------------------
// This bool function checks to see if the current signal strength is
// stronger than the one specified.
//-------------------------------------------------------------------------

procedure bool COMM_SIGNAL_STRONG_ENOUGH with
   double threshold;
header
body
{
    CSB_CommInfoList_t commInfoList;
    double averageSignalStrength, sum;
    int i, numConnections;

    commInfoList = exec_get_csb_comm_info();

    output = false;
    numConnections = 0;
    sum = 0;

    for (i = 0; i < (int)(commInfoList.size()); i++)
    {
        if (commInfoList[i].nodeType == CSB_NODE_OTHER)
        {
            sum += commInfoList[i].signalStrength;
            numConnections++;
        }
    }

    if (numConnections > 0)
    {
        averageSignalStrength = sum/(double)numConnections;

        if (averageSignalStrength > threshold)
        {
            output = true;
        }
    }
}         
pend

//-------------------------------------------------------------------------
// This bool function checks to see if the behavior advise confidence is
// higher than the one specified.
//-------------------------------------------------------------------------

procedure bool CSB_CONFIDENCE_HIGH_ENOUGH with
   int method;
   double threshold;
header
body
{
    CSB_Advise_t advise;

    // Get the advise.
    advise = exec_get_csb_advise(method);

    if (advise.confidence > threshold)
    {
        output = true;
    }
    else
    {
        output = false;
    }
}         
pend

/**********************************************************************
# $Log: COMMUNICATION_SENSITIVE_BEHAVIOR.cnl,v $
# Revision 1.1.1.1  2006/07/12 13:37:58  endo
# MissionLab 7.0
#
# Revision 1.1  2006/07/02 21:47:07  endo
# Communication sensitive behavior from MARS 2020 migrated into AO-FNC repository.
#
# Revision 1.3  2004/09/15 06:24:11  endo
# FollowCSBAdvise now takes the advise method as an clickable option.
#
# Revision 1.2  2004/04/17 12:25:19  endo
# Intalling JBox Sensor Update.
#
# Revision 1.1  2004/03/23 08:01:08  endo
# Initial revision.
#
#**********************************************************************/
