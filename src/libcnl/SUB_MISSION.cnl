/**********************************************************************
 **                                                                  **
 **                           SUB_MISSION.cnl                        **
 **                                                                  **
 **  Written by:  Yoichiro Endo                                      **
 **                                                                  **
 **  StartSubMission behavior will be used as a start of a new       **
 **  sub-mission. It should be used with SubMissionReady trigger as  **
 **  a pair. CBRPlanner will treat this behavior as a marker to      **
 **  break-down the mission into sub-missions upon storage.          **
 **                                                                  **
 **  Copyright 2003 - 2006, Georgia Tech Research Corporation        **
 **  Atlanta, Georgia  30332-0415                                    **
 **  ALL RIGHTS RESERVED, See file COPYRIGHT for details.            **
 **                                                                  **
 **********************************************************************/

/* $Id: SUB_MISSION.cnl,v 1.1.1.1 2006/07/12 13:37:59 endo Exp $ */

#include "cnl.inc"

init
#include <math.h>
iend

/***********************************************************************/

procedure Vector START_SUB_MISSION with
   CNLString_t sub_mission_name;
   int deployment_method;
   Vector goal_rel_loc;
header
body
{
    Vector goal;
    robot_position robotPos;
    double thetaRad, thetaDeg;

    VECTOR_CLEAR(output);
    VECTOR_CLEAR(goal);

    switch (deployment_method) {

    case DEPLOY_BY_GOTO:
        if(len_3d(goal_rel_loc) > EPS_ZERO)
        {
            // generate a vector towards the goal
            output = goal_rel_loc;
            unit_3d(output);
        }
        break;

    case DEPLOY_BY_LOCALIZE:
        if (len_3d(goal_rel_loc) > EPS_ZERO)
        {
            robotPos = exec_get_position();
            rotate_z(goal_rel_loc, robotPos.heading);
            add_2d(goal_rel_loc, robotPos.v, goal);
            thetaRad = atan2(goal_rel_loc.y, goal_rel_loc.x);
            thetaDeg = RADIANS_TO_DEGREES(thetaRad);
            exec_setxyTheta((double)(goal.x), (double)(goal.y), thetaDeg);
        }
        break;
    }
}
pend

/***********************************************************************/

procedure bool SUB_MISSION_READY with
   int sub_mission_deployment;
   boolean at_goal_trigger;
header
body
   switch (sub_mission_deployment) {

   case DEPLOY_FIRST:
       output = at_goal_trigger;
       break;

   case EXECUTE_IMMEDIATELY:
   default:
       output = true;
       break;
   }
pend

/**********************************************************************
# $Log: SUB_MISSION.cnl,v $
# Revision 1.1.1.1  2006/07/12 13:37:59  endo
# MissionLab 7.0
#
# Revision 1.3  2006/02/19 17:54:53  endo
# Experiment related modifications
#
# Revision 1.2  2006/01/10 06:16:04  endo
# AO-FNC Type-I check-in.
#
# Revision 1.1.1.1  2005/02/06 23:00:06  endo
# AO-FNC Project (based on mlab-6.0.01.06192003.tar.gz)
#
# Revision 1.1  2003/04/06 09:25:40  endo
# Initial revision
#
#**********************************************************************/
