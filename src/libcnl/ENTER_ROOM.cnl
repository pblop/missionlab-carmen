/**********************************************************************
 **                                                                  **
 **                      ENTER_ROOM.cnl                              **
 **                                                                  **
 **                                                                  **
 **  Written by:  Jonathan F. Diaz                                   **
 **                                                                  **
 **  Copyright 1999, Georgia Tech Research Corporation               **
 **  Atlanta, Georgia  30332-0415                                    **
 **  ALL RIGHTS RESERVED, See file COPYRIGHT for details.            **
 **                                                                  **
 **********************************************************************/

/* $Id: ENTER_ROOM.cnl,v 1.1.1.1 2006/07/12 13:37:58 endo Exp $ */

#include "cnl.inc"

init
iend

procedure Vector ENTER_ROOM with 
double distance;
int unmarked;
header
body
{
  Vector pt1, pt2, mid, inpt;
  robot_position loc;
  double dx, dy, len, dockxy, cone_angle, controlled_radius;
  double l1, l2;
  boolean stat;
  int newtime;

  VECTOR_CLEAR(output);

  if (unmarked == ENTER_UNMARKED_ONLY) 
    stat = exec_detect_nearest_unmarked_doorway(&pt1, &pt2);
  else if (unmarked == ENTER_ANY) 
    stat = exec_detect_nearest_doorway(&pt1, &pt2, &newtime);
  else stat = exec_detect_nearest_doorway(&pt1, &pt2, &newtime);

  if (stat == TRUE) {
    /*determine the midpoint*/
    mid.x = (pt1.x + pt2.x)/2.0; 
    mid.y = (pt1.y + pt2.y)/2.0;
    /*calculate the slope of perpendicular line*/
    dy = pt1.x - pt2.x; 
    dx = pt1.y - pt2.y;
    dy *= -1;
    /*normalize the length to be 0.5*/
    len = 0.5/sqrt(dx*dx + dy*dy);
    dx *= len;
    dy *= len;
    /* calculate a test point */
    inpt.x = mid.x + dx;
    inpt.y = mid.y + dy;
    /* Determine which side of the door is within a room */
    loc.heading = atan2(inpt.y - mid.y, inpt.x - mid.x);
    loc.v.x = inpt.x;
    loc.v.y = inpt.y;
    printf("Entering room (%f %f)<-->(%f %f) m: (%f %f) loc: (%f %f %f)\n",
	   pt1.x, pt1.y, pt2.x, pt2.y, mid.x, mid.y, loc.v.x,
	   loc.v.y, loc.heading);
    
    if (exec_detect_in_room(loc) == FALSE) {
      /* The otherside of the doorway must be inside a room. Move
       the point distance units into the room*/
      inpt.x = mid.x - dx*2*distance;
      inpt.y = mid.y - dy*2*distance;
      /*printf("False inpt --> (%f %f)\n", inpt.x, inpt.y);*/
    }
    else {
      /* inpt was inside the room scale by distance */
      inpt.x = mid.x + dx*2*distance;
      inpt.y = mid.y + dy*2*distance;
      printf("True inpt --> (%f %f)\n", inpt.x, inpt.y);
    }
    /* Dock angle is the angle from the center point to
       the midpoint of the doorway */
    dockxy = atan2(mid.y - inpt.y, mid.x - inpt.x);
    /* Cone angle is twice the angle from the center point
       to the doorway midpoint to either endpoint */
    l1 = sqrt((pt1.x - mid.x)*(pt1.x - mid.x) + (pt1.y - mid.y)*(pt1.y - mid.y));
    l2 = sqrt((inpt.x - mid.x)*(inpt.x - mid.x) + (inpt.y - mid.y)*(inpt.y - mid.y));
    cone_angle = 2*fabs(atan2(l1, l2));
    /* Controlled radius is the distance from the center to any point
       on the circle. */
    controlled_radius = sqrt((pt1.x - inpt.x)*(pt1.x - inpt.x) +
			     (pt1.y - inpt.y)*(pt1.y - inpt.y)) + 1.0;
    loc = exec_get_position();
    
    /* Call DOCK with both ballistic and controlled gains set to 1.0 */
    output = compute_dock_force_on_robot(loc.v, inpt, dockxy, controlled_radius,
					 cone_angle, 1.0, 1.0);
    rotate_z(output, -1*loc.heading);
    output.z = 0.0;
  }
  else {
    fprintf(stderr, "No doorways around to enter\n");
  } 
}
pend

/**********************************************************************
 * $Log: ENTER_ROOM.cnl,v $
 * Revision 1.1.1.1  2006/07/12 13:37:58  endo
 * MissionLab 7.0
 *
 * Revision 1.1.1.1  2005/02/06 23:00:03  endo
 * AO-FNC Project (based on mlab-6.0.01.06192003.tar.gz)
 *
 * Revision 1.4  2000/09/19 10:01:36  endo
 * Code modified at Rockville, MD, for TMR demo (August).
 *
 **********************************************************************/
